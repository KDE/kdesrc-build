#autoload

local expl
local -aU kdesrc_build_modules

# The output of `kdesrc-build -M --query module-set` is in the format:
# extra-cmake-modules: frameworks
# Where "extra-cmake-modules" is module name,
# ": " is output separator,
# "frameworks" is module-set name, containing the module

if _cache_invalid kdesrc_build_modules || ! _retrieve_cache kdesrc_build_modules ; then
  # strip ":*" suffix (i.e. everything after and including first column).
  # (@) array Expansion Flags are needed because we need to operate on each element separately.
  kdesrc_build_modules=( "${(@)${(f)"$(
    _call_program kdesrc-build-modules \
      kdesrc-build -M --query module-set
    )"}%%:*}" )
  _store_cache kdesrc_build_modules kdesrc_build_modules
fi

_wanted modules expl "module" \
  compadd -a kdesrc_build_modules
