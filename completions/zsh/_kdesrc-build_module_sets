#autoload

local expl
local -aU kdesrc_build_module_sets

# The output of `kdesrc-build -M --query module-set` is in the format:
# extra-cmake-modules: frameworks
# Where "extra-cmake-modules" is module name,
# ": " is output separator,
# "frameworks" is module-set name, containing the module

if _cache_invalid kdesrc_build_modulesets || ! _retrieve_cache kdesrc_build_modulesets ; then
  # strip "*: " prefix (i.e. everything before column and including the column and space after it).
  # (@) array Expansion Flags are needed because we need to operate on each element separately.
  kdesrc_build_module_sets=( "${(@)${(f)"$(
    _call_program kdesrc-build-module-sets \
      kdesrc-build -M --query module-set
    )"}#*: }" )
  _store_cache kdesrc_build_module_sets kdesrc_build_module_sets
fi

_wanted module_sets expl "module-set" \
  compadd -a kdesrc_build_module_set
