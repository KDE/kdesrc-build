#compdef kdesrc-build

# SPDX-FileCopyrightText: 2022 ivan tkachenko <me@ratijas.tk>
#
# SPDX-License-Identifier: GPL-2.0-or-later


# See https://docs.kde.org/trunk5/en/kdesrc-build/kdesrc-build/supported-cmdline-params.html

_arguments \
  '(- *)'--help'[Displays help on commandline options]' \
  '(- *)'{-v,--version}'[Displays version information]' \
  \
  '(--pretend -p)'{--pretend,-p}"[Don't actually take major actions, instead describe what would be done]" \
  '--list-build[List what modules would be built in the order in which they would be built]' \
  '--dependency-tree[Print out dependency information on the modules that would be built, using a `tree` format]' \
  '(--src-only --no-src)'{--src-only,--no-src}'[Only perform/Skip update source code]' \
  '--refresh-build[Start the build from scratch]' \
  '--rc-file=[Read configuration from filename instead of default]:::_files' \
  '--initial-setup[Installs Plasma env vars (~/.bashrc), required system pkgs, and a base kdesrc-buildrc]' \
  '--install-distro-packages[Installs required system pkgs]' \
  '--generate-config[Installs a base kdesrc-buildrc]' \
  '--update-shellrc[Installs Plasma env vars (~/.bashrc)]' \
  '(--resume-from --resume-after)'{--resume-from=,--resume-after=}'[Skips modules until just before or after the given package, then operates as normal]:::_kdesrc-build_modules' \
  '(--stop-before --stop-after)'{--stop-before=,--stop-after=}'[Stops just before or after the given package is reached]:::_kdesrc-build_modules' \
  '(--include-dependencies --no-include-dependencies)'{--include-dependencies,--no-include-dependencies}'[Builds/Skip KDE-based dependencies]' \
  '(--stop-on-failure --no-stop-on-failure)'{--stop-on-failure,--no-stop-on-failure}'[Stops/Does not stop the build as soon as a package fails to build]' \
  \
  '(--quiet -q)'{--quiet,-q}'[Do not be as noisy with the output.]' \
  '--really-quiet[Only output warnings and errors.]' \
  '--verbose[Be very descriptive about what is going on, and what kdesrc-build is doing.]' \
  '--show-info[Displays information about kdesrc-build and the operating system.]' \
  '(--color --no-color)--color[Toggle colorful output]' \
  \
  '(--metadata-only --no-metadata)'{--metadata-only,--no-metadata}'[Only perform/Skip the metadata download process]' \
  '(--build-only --no-build)'{--build-only,--no-build}'[Only perform/Skip the build process.]' \
  '(--install-only --no-install)'{--install-only,--no-install}'[Only perform/Skip the install process]' \
  \
  '--rebuild-failures[Only those modules which failed to build on a previous run.]' \
  '--force-build[Disable skipping the build process.]' \
  '--resume[Resum after a build failure]' \
  \
  '*:: :_kdesrc-build_modules'
